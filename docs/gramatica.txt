Program -> StmtList EOF

StmtList -> Stmt StmtList | ε

Stmt -> Decl | Assign | If | While | For | Print | Block

Decl -> var id DeclTail | let id DeclTail | int id DeclTail | float id DeclTail
DeclTail -> = BoolExpr ; | ;

Assign -> AssignNoSemi ;
AssignNoSemi -> id = BoolExpr

If -> if ( BoolExpr ) Stmt ElseOpt
ElseOpt -> else Stmt | ε

While -> while ( BoolExpr ) Stmt

For -> for ( ForInit ; BoolExpr ; AssignNoSemi ) Stmt
ForInit -> let id = BoolExpr | var id = BoolExpr | id = BoolExpr

Print -> print ( BoolExpr ) ;

Block -> { StmtList }

BoolExpr -> Expr BoolExprPrime
BoolExprPrime -> < Expr BoolExprPrime | > Expr BoolExprPrime | == Expr BoolExprPrime | ε

Expr -> Term ExprPrime
ExprPrime -> + Term ExprPrime | - Term ExprPrime | ε

Term -> Factor TermPrime
TermPrime -> * Factor TermPrime | / Factor TermPrime | ε

Factor -> ( BoolExpr ) | id | number

id -> IDENTIFIER
number -> NUMBER